/*
* SPL; AS3 Spelling library for Flash and the Flex SDK. 
* 
* Copyright (c) 2013 gskinner.com, inc.
* 
* Permission is hereby granted, free of charge, to any person
* obtaining a copy of this software and associated documentation
* files (the "Software"), to deal in the Software without
* restriction, including without limitation the rights to use,
* copy, modify, merge, publish, distribute, sublicense, and/or sell
* copies of the Software, and to permit persons to whom the
* Software is furnished to do so, subject to the following
* conditions:
* 
* The above copyright notice and this permission notice shall be
* included in all copies or substantial portions of the Software.
* 
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
* OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
* NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
* HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
* WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
* FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*/
package com.gskinner.spelling {
	
	import flash.events.Event;
	import flash.net.URLRequest;
	
	import mx.core.UIComponent;
	
	/**
	 * The SPLWordListLoader component provides a visual way to load and set the master word list
	 * on the SpellingDictionary class. This class should only be used in MXML. For use directly
	 * with ActionScript, please use WordListLoader.as.
	 */
	 [IconFile("componentIcon.png")]
	public class SPLWordListLoaderFlex extends UIComponent {
		
		/**
		 * Indicates if the WordListLoader has been initialized already.
		 */
		protected static var inited:Boolean=false;
		
		/** @private */
		protected var _wordListLoader:WordListLoader;
		
		/** @private */
		protected var urlIsInvalid:Boolean;
		
		/** @private */
		protected var _url:String;
		
		/** @private */
		public function SPLWordListLoaderFlex() {
			if (inited) { throw(new Error("Only one instance of SPLWordListLoader can be instantiated.")); }
			
			inited = true;
			_wordListLoader = new WordListLoader();
			visible = false;
		}
		
		[Inspectable(defaultValue="wordlist.gspl")]
		/** @private */
		public function set url(value:String):void {
			if (value == "" || value == null) {
				throw "Error: No url parameter set on SPLLoader component!";
			}
			
			_url = value;
			urlIsInvalid = true;
			this.invalidateProperties();
		}
		
		/**
		 * Returns the WordListLoader instance that was generated by this component. Use this
		 * to change properties or subscribe to events of the WordListLoader.
		 */
		public function get wordListLoader():WordListLoader {
			return _wordListLoader;
		}
		
		/** @private */
		override protected function commitProperties():void {
			if (urlIsInvalid) {
				urlIsInvalid = false;
				_wordListLoader = new WordListLoader(new URLRequest(_url));
				_wordListLoader.addEventListener(Event.COMPLETE,handleComplete);
			}
			super.commitProperties();
		}
		
		/** @private */
		protected function handleComplete(evt:Event):void {
			_wordListLoader.removeEventListener(Event.COMPLETE, handleComplete);
			// delay one frame to minimize stalling:
			callLater(setWordList);
		}
		
		/** @private */
		protected function setWordList():void {
			SpellingDictionary.getInstance().setMasterWordList(_wordListLoader.data);
		}
	}
}